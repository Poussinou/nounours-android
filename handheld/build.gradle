apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 3
        targetSdkVersion 23
        versionName "3.0.0"
        versionCode 300
        archivesBaseName += "-" + versionName
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    productFlavors {
        fullFoss {
            applicationId "ca.rmen.nounours"
            resValue "string", "DEFAULT_THEME_ID", "0"
        }

        fullWear {
            applicationId "ca.rmen.nounours"
            resValue "string", "DEFAULT_THEME_ID", "0"
        }

        bugdroid {
            applicationId "ca.rmen.nounours.lwp.robot"
            resValue "string", "DEFAULT_THEME_ID", "5004"
        }

        nounours {
            applicationId "ca.rmen.nounours.lwp.rainbow"
            resValue "string", "DEFAULT_THEME_ID", "5000"
        }

        all {
            resValue "string", "APPLICATION_ID", applicationId
        }
    }

    // https://code.google.com/p/android/issues/detail?id=152198
    // lint will complain about the resValue strings not being translated.
    lintOptions {
        // Set this issue to 'error' severity instead of 'fatal' severity.
        error 'MissingTranslation'
    }

    sourceSets {
        bugdroid {
            assets.srcDirs = ['../common/src/bugdroid/assets']
        }
        nounours {
            assets.srcDirs = ['../common/src/nounours/assets']
        }
        fullFoss {
            assets.srcDirs = ['../common/src/bugdroid/assets', '../common/src/nounours/assets']
            res.srcDirs = ['src/full/res']
        }
        fullWear {
            assets.srcDirs = ['../common/src/bugdroid/assets', '../common/src/nounours/assets']
            res.srcDirs = ['src/full/res']
        }
    }

    File signFile = rootProject.file('signing.properties')
    if (signFile.exists()) {
        Properties p = new Properties();
        p.load(new FileInputStream(signFile))

        signingConfigs {
            release {
                keyAlias p.KeyAlias
                keyPassword p.KeyPassword
                storeFile file(p.StoreFile)
                storePassword p.StorePassword
            }
        }
        android.buildTypes.release.signingConfig =signingConfigs.release
    } else  {
        android.buildTypes.release.signingConfig = null
    }

}

dependencies {
    compile "ca.rmen:libnounours:2.0.1"
    compile 'com.robbypond:android-ColorPickerPreference:1.11.1'
    compile project(':common')
}
